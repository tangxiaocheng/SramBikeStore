plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id('scabbard.gradle') version '0.4.0'
    id 'org.jlleitschuh.gradle.ktlint'
}

apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'app.sram.bikestore'
        buildConfigField 'String', 'SERVER_URL', "${server_url}"
        buildConfigField 'String', 'GOOGLE_KEY', "${google_maps_key}"
        resValue "string", "google_maps_key", "${google_maps_key}"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

scabbard {
    enabled true
    failOnError true
    fullBindingGraphValidation true
    qualifiedNames false
    outputFormat 'png'
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'com.google.code.gson:gson:2.8.6'

    def navigationVersion = '2.3.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    def okhttp3Version = '5.0.0-alpha.2'
    implementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3Version"

    def retrofit2Version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2Version"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:1.4.0'

    def roomVersion = '2.2.6'
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    def chuckerVersion = '3.4.0'
    debugImplementation "com.github.chuckerteam.chucker:library:$chuckerVersion"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chuckerVersion"

    def daggerVersion = '2.32'
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // For image loading.
    implementation("io.coil-kt:coil:1.1.1")


    // Google map
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    testImplementation 'io.mockk:mockk:1.10.6'
    testImplementation 'junit:junit:4.13.2'

    // For fixing up mockito mock problem
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.mockito:mockito-core:3.7.0'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    testImplementation 'com.google.truth:truth:1.1.2'

    //A log library
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //A Rxjava library for monitoring network connectivity
    implementation 'com.github.pwittchen:reactivenetwork-rx2:3.0.8'

    //A library for easy permission management.
    implementation 'com.github.tbruyelle:rxpermissions:0.11'

    testImplementation 'commons-io:commons-io:20030203.000550'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


    // Pull to refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // A delay interceptor library for delay network request
    implementation 'com.github.pawel-schmidt:okhttp-delay-interceptor:0.0.1'

    //noinspection GradleDependency  "Android SDK 29 requires Java 9 (have Java 8)."
    testImplementation 'org.robolectric:robolectric:4.5.1'


}

ktlint {
    android = true
    reporters {
        reporter 'plain'
        reporter 'checkstyle'
    }
    outputToConsole = true
    outputColorName.set('RED')
}
